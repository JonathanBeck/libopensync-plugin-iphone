# - Try to find libplist 
# Find libplist headers, libraries and the answer to all questions.
#
#  LIBPLIST_FOUND               True if libplist got found
#  LIBPLIST_INCLUDE_DIRS        Location of libplist headers 
#  LIBPLIST_LIBRARIES           List of libaries to use libplist 
#
# Copyright (c) 2009 Jonathan Beck <jonabeck@gmail.com>
#
#  Redistribution and use is allowed according to the terms of the New
#  BSD license.
#  For details see the accompanying COPYING-CMAKE-SCRIPTS file.
#

INCLUDE( FindPkgConfig )

IF ( LIBPLIST_FIND_REQUIRED )
	SET( _pkgconfig_REQUIRED "REQUIRED" )
ELSE( LIBPLIST_FIND_REQUIRED )
	SET( _pkgconfig_REQUIRED "" )	
ENDIF ( LIBPLIST_FIND_REQUIRED )

IF ( LIBPLIST_MIN_VERSION )
	PKG_SEARCH_MODULE( LIBPLIST ${_pkgconfig_REQUIRED} libplist-1.0>=${LIBPLIST_MIN_VERSION} )
ELSE ( LIBPLIST_MIN_VERSION )
	PKG_SEARCH_MODULE( LIBPLIST ${_pkgconfig_REQUIRED} libplist-1.0 )
ENDIF ( LIBPLIST_MIN_VERSION )


IF( NOT LIBPLIST_FOUND AND NOT PKG_CONFIG_FOUND )
	FIND_PATH( LIBPLIST_INCLUDE_DIRS libplist/libplist.h )
	FIND_LIBRARY( LIBPLIST_LIBRARIES libplist)

	# Report results
	IF ( LIBPLIST_LIBRARIES AND LIBPLIST_INCLUDE_DIRS )	
		SET( LIBPLIST_FOUND 1 )
		IF ( NOT LIBPLIST_FIND_QUIETLY )
			MESSAGE( STATUS "Found libplist: ${LIBPLIST_LIBRARIES}" )
		ENDIF ( NOT LIBPLIST_FIND_QUIETLY )
	ELSE ( LIBPLIST_LIBRARIES AND LIBPLIST_INCLUDE_DIRS )	
		IF ( LIBPLIST_FIND_REQUIRED )
			MESSAGE( SEND_ERROR "Could NOT find libplist" )
		ELSE ( LIBPLIST_FIND_REQUIRED )
			IF ( NOT LIBPLIST_FIND_QUIETLY )
				MESSAGE( STATUS "Could NOT find libplist" )	
			ENDIF ( NOT LIBPLIST_FIND_QUIETLY )
		ENDIF ( LIBPLIST_FIND_REQUIRED )
	ENDIF ( LIBPLIST_LIBRARIES AND LIBPLIST_INCLUDE_DIRS )
ENDIF( NOT LIBPLIST_FOUND AND NOT PKG_CONFIG_FOUND )

IF ( LIBPLIST_FIND_REQUIRED AND NOT LIBPLIST_FOUND)
	MESSAGE( FATAL_ERROR "Could NOT find libplist")
ENDIF ( LIBPLIST_FIND_REQUIRED AND NOT LIBPLIST_FOUND)

IF ( LIBPLIST_FOUND AND NOT LIBPLIST_FIND_QUIETLY )
	MESSAGE( STATUS "Found libplist: ${LIBPLIST_LIBRARIES}" )
ENDIF ( LIBPLIST_FOUND AND NOT LIBPLIST_FIND_QUIETLY )

MARK_AS_ADVANCED( LIBPLIST_LIBRARIES LIBPLIST_INCLUDE_DIRS )
